plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.0'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.graalvm.buildtools.native' version '0.10.6'
    id 'gg.jte.gradle' version '3.2.1'
    id 'com.diffplug.spotless' version '7.0.3'
    id 'pl.allegro.tech.build.axion-release' version '1.18.18'
}

scmVersion {
    tag {
        prefix.set("")
    }
}

group = 'net.binarypaper'
version = scmVersion.version

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.hibernate.orm:hibernate-community-dialects'
    runtimeOnly 'org.xerial:sqlite-jdbc:3.49.1.0'
    implementation 'org.liquibase:liquibase-core'
    implementation 'gg.jte:jte-spring-boot-starter-3:3.2.1'
    jteGenerate 'gg.jte:jte-native-resources:3.2.1'
    implementation 'io.github.gadnex:jte-localizer-spring-boot-starter:1.0.3'
    implementation 'io.github.gadnex:jte-datastar-spring-boot-starter:0.1.2'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

jte {
    generate()
    binaryStaticContent = true
    jteExtension("gg.jte.nativeimage.NativeResourcesExtension")
}

jar {
    dependsOn precompileJte
    from fileTree("jte-classes") {
        include "**/*.class"
        include "**/*.bin" // Only required if you use binary templates
    }
}

tasks.named('test') {
    useJUnitPlatform()
    jvmArgs "-javaagent:${configurations.testRuntimeClasspath.find { it.name.contains('mockito-core') }}", '-Xshare:off'
}

spotless {
    java {
        googleJavaFormat()
        target 'src/*/java/**/*.java'
    }
}